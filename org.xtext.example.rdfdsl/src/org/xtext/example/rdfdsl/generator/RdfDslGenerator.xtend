/*
 * generated by Xtext 2.24.0
 */
package org.xtext.example.rdfdsl.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.xtext.example.rdfdsl.rdfDsl.Model
import org.xtext.example.rdfdsl.rdfDsl.Namespace
import org.xtext.example.rdfdsl.rdfDsl._Class
import org.xtext.example.rdfdsl.rdfDsl._Property
import org.xtext.example.rdfdsl.rdfDsl.Cardinality
import org.xtext.example.rdfdsl.rdfDsl._Float
import org.xtext.example.rdfdsl.rdfDsl._Integer
import org.xtext.example.rdfdsl.rdfDsl._String
import org.xtext.example.rdfdsl.rdfDsl.Type
import org.xtext.example.rdfdsl.rdfDsl.ClassRef

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class RdfDslGenerator extends AbstractGenerator {

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val model = resource.allContents.filter(Model).next
		fsa.generateFile('model.py', model.generate)
	}

	def dispatch String generate(Model model) '''
		import rdflib as rdf		
		RDF  = rdf.Namespace('http://www.w3.org/1999/02/22-rdf-syntax-ns#')
		RDFS = rdf.Namespace('http://www.w3.org/2000/01/rdf-schema#')
		OWL  = rdf.Namespace('http://www.w3.org/2002/07/owl#')
		XSD  = rdf.Namespace('http://www.w3.org/2001/XMLSchema#')
		
		g = rdf.Graph()
		g.bind('rdf', RDF)
		g.bind('rdfs', RDFS)
		g.bind('owl', OWL)
		g.bind('xsd', XSD)
		
		«FOR namespace : model.namespaces»
			«namespace.generate»
		«ENDFOR»
		
		print(g.serialize(format="turtle").decode("utf-8"))
	'''

	def dispatch String generate(Namespace namespace) '''
		«IF namespace.link.replace('"', '').endsWith('#')»		
			ns = rdf.Namespace(«namespace.link»)
		«ELSE»
			ns = rdf.Namespace("«namespace.link.replace('"', '')+"#"»")
		«ENDIF»
		g.bind('«namespace.name»', ns)
		
		«FOR _class : namespace.classes»
			«_class.generate»
		«ENDFOR»
	'''

	def dispatch String generate(_Class _class) '''
		«IF _class.superClass === null»
			parent = OWL.Class
			_type = RDF.type
		«ELSE»
			parent = ns['«_class.superClass»']
			_type = RDFS.subClassOf
		«ENDIF»		
		_class = ns['«_class.name»']
		g.add((_class, _type, parent))
		
		«FOR property : _class.properties»
			«property.generate»
		«ENDFOR»
	'''

	def dispatch String generate(_Property property) '''
		prop = ns['«property.name»']
		prop_type = «IF property.type instanceof ClassRef»OWL.ObjectProperty«ELSE»OWL.DataProperty«ENDIF»
		g.add((prop, RDF.type, prop_type))
		g.add((prop, RDFS.domain, _class))
		g.add((prop, RDFS.range, «property.type.generate»))
		
		«IF property.cardinality !== null»
			«property.cardinality.generate»
		«ENDIF»
	'''

	def dispatch String generate(Cardinality cardinality) '''
		cardmin_entity = ns['_%s_%s_cardmin' % (_class.split('#')[-1], prop.split('#')[-1])]
		g.add( (_class, OWL.equivalentClass, cardmin_entity) )
		g.add( (cardmin_entity, RDF.type, OWL.Restriction) )
		g.add( (cardmin_entity, OWL.onProperty, prop) )
		g.add( (cardmin_entity, OWL.minCardinality, rdf.Literal(«cardinality.min», datatype=XSD.integer)) )
		
		«IF cardinality.max != '*'»
			cardmax_entity = ns['_%s_%s_cardmax' % (_class.split('#')[-1], prop.split('#')[-1])]
			g.add( (_class, OWL.equivalentClass, cardmax_entity) )
			g.add( (cardmax_entity, RDF.type, OWL.Restriction) )
			g.add( (cardmax_entity, OWL.onProperty, prop) )
			g.add( (cardmax_entity, OWL.maxCardinality, rdf.Literal(«cardinality.max», datatype=XSD.integer)) )
		«ENDIF»
		
	'''

	def dispatch String generate(_Float type) '''XSD.float'''

	def dispatch String generate(_Integer type) '''XSD.integer'''

	def dispatch String generate(_String type) '''XSD.string'''

	def dispatch String generate(ClassRef type) '''ns['«type.id»']'''

}
